{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "9c561951-7f20-4401-a20c-f1e391bc69f1",
   "metadata": {},
   "outputs": [],
   "source": [
    "using DifferentialEquations\n",
    "using Distributions\n",
    "using LinearAlgebra\n",
    "using Maybe\n",
    "using Random\n",
    "using Plots"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c5cf3ab7-05c1-4f8f-9056-d317086fb829",
   "metadata": {},
   "source": [
    "# Soft Kilobot Model"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "6da5429f-9ccc-4c29-82db-2ceee37d0665",
   "metadata": {},
   "outputs": [],
   "source": [
    "VEL_X   = 0;\n",
    "VEL_Y   = 1;\n",
    "POS_X   = 2;\n",
    "POS_Y   = 3;\n",
    "FIELDS  = 4;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "63ee3f2d-31c5-44c5-b73e-e499003a7c7b",
   "metadata": {},
   "outputs": [],
   "source": [
    "mutable struct SoftKilobot\n",
    "    N_SIDE::Int               # Robots per side\n",
    "    MASS::Float64             # Robot mass [kg]\n",
    "    V_MAX::Float64            # Max speed\n",
    "    V_DAMP::Float64           # Speed damping\n",
    "    S_REST::Float64           # Spring rest length [m]\n",
    "    S_STIFF::Float64          # Spring stiffness\n",
    "    S_DAMP::Float64           # Spring damping\n",
    "    N_FACTOR::Maybe.T{Normal} # Noise factor on velocity\n",
    "    n::Int                    # Number of robots\n",
    "    neighs::Array{Array{Int}} # Neighbors\n",
    "    v_bias::Matrix{Float64}   # Speed bias\n",
    "    v_old::Matrix{Float64}    # Old velocity\n",
    "    SoftKilobot(n_side,mass,v_max,v_damp,s_rest,s_stiff,s_damp,n_factor) = (\n",
    "        y = new(n_side,mass,v_max,v_damp,s_rest,s_stiff,s_damp,n_factor);\n",
    "        y.n = n_side^2;\n",
    "        y.neighs = [ Int[] for k in 1:y.n ];\n",
    "        y.v_bias = zeros(2, y.n);\n",
    "        y.v_old = zeros(2, y.n);\n",
    "        return y)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "7a26e614-f57f-47f8-8b84-8a6f09b8b307",
   "metadata": {},
   "outputs": [],
   "source": [
    "function SoftKilobot_Init(\n",
    "        cntr::Vector{Float64},                   # Center of the robot distribution\n",
    "        ornt::Float64,                           # Orientation of robot distribution (degrees)\n",
    "        n_side::Int,                             # Robots per side\n",
    "        mass::Float64,                           # Robot mass\n",
    "        v_max::Float64,                          # Max speed\n",
    "        v_damp::Float64,                         # Speed damping\n",
    "        s_rest::Float64,                         # Spring rest length\n",
    "        s_stiff::Float64,                        # Spring stiffness\n",
    "        s_damp::Float64,                         # Spring damping\n",
    "        n_factor::Maybe.T{Normal{Float64}},      # Noise factor on velocity\n",
    "        n_bias_length::Maybe.T{Normal{Float64}}, # Noise on velocity bias vector length\n",
    "        n_bias_angle::Maybe.T{Normal{Float64}})  # Noise on velocity bias vector angle\n",
    "    # Create empty spring system\n",
    "    ss = SoftKilobot(n_side,mass,v_max,v_damp,s_rest,s_stiff,s_damp,n_factor)\n",
    "    # Add spring links\n",
    "    for k in 1:ss.n\n",
    "        # Horizontal\n",
    "        kk = (k - 1) % n_side\n",
    "        if(kk > 0)        push!(ss.neighs[k],k-1) end\n",
    "        if(kk < n_side-1) push!(ss.neighs[k],k+1) end\n",
    "        # Vertical\n",
    "        kk = floor(Int, (k - 1) / n_side)\n",
    "        if(kk > 0)        push!(ss.neighs[k],k-n_side) end\n",
    "        if(kk < n_side-1) push!(ss.neighs[k],k+n_side) end\n",
    "    end\n",
    "    # Calculate rotation matrix\n",
    "    ornt *= pi / 180.0\n",
    "    rm = [cos(ornt) -sin(ornt); sin(ornt) cos(ornt)]\n",
    "    # Add robots, the ids grow with positive x and negative y\n",
    "    #    +x ->\n",
    "    # -y 1 2 3\n",
    "    #  | 4 5 6\n",
    "    #  v 7 8 9\n",
    "    x = zeros(FIELDS, ss.n)\n",
    "    for i in 0:(n_side-1)\n",
    "        for j in 0:(n_side-1)\n",
    "            k = i + n_side * j + 1\n",
    "            px = rm * (s_rest * [i, j]) + cntr\n",
    "            x[:, k] = [0.0 0.0 px[1] px[2]]\n",
    "            if n_factor != nothing\n",
    "                @? v_bias = [ abs(rand(n_bias_length)); 0.0 ]\n",
    "                @? v_bias_angle = rand(n_bias_angle)\n",
    "                @? v_bias_rot = [ cos(v_bias_angle) -sin(v_bias_angle); sin(v_bias_angle) cos(v_bias_angle) ]\n",
    "                @? ss.v_bias[:, k] = v_bias_rot * v_bias\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return ss, x\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "30bd7a6a-f4d8-4930-b53b-0c2c7c5db7f7",
   "metadata": {},
   "outputs": [],
   "source": [
    "function SoftKilobot_CalcForces(u, ss)\n",
    "    fs = zeros(2,ss.n)\n",
    "    for k in 1:ss.n\n",
    "        # Robot index in state vector to identify block\n",
    "        idx1 = (k-1) * FIELDS + 1\n",
    "        # Go through neighbors\n",
    "        for n in ss.neighs[k]\n",
    "            # Avoid useless recalculations\n",
    "            if k < n\n",
    "                # Robot index in state vector to identify block\n",
    "                idx2 = (n-1) * FIELDS + 1\n",
    "                # Get positions\n",
    "                p1 = u[POS_X + idx1:POS_Y + idx1,:]\n",
    "                p2 = u[POS_X + idx2:POS_Y + idx2,:]\n",
    "                # Calculate relative vector\n",
    "                p12 = p2 - p1\n",
    "                # Calculate distance\n",
    "                dist = norm(p12)\n",
    "                # Calculate force contribution and add it\n",
    "                f = ss.S_STIFF * (p12 / dist) * (dist - ss.S_REST)\n",
    "                fs[:,k] += f\n",
    "                fs[:,n] -= f\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return fs\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "52d8c028-ab30-48bd-9f0b-021caa25a964",
   "metadata": {},
   "outputs": [],
   "source": [
    "function SoftKilobot_UpdateState(du, u, ss, v_tgt, fs)\n",
    "    for k in 1:ss.n\n",
    "        # Robot index in state vector to identify block\n",
    "        idx = (k-1) * FIELDS + 1\n",
    "        # State update\n",
    "        du[VEL_X + idx] = 0.0\n",
    "        if u[VEL_X + idx] > -ss.V_MAX && u[VEL_X + idx] < ss.V_MAX\n",
    "            du[VEL_X + idx] = -ss.V_DAMP * u[VEL_X + idx] + (v_tgt[1] - u[VEL_X + idx]) / ss.MASS + fs[1,k] / ss.MASS\n",
    "        end\n",
    "        du[VEL_Y + idx] = 0.0\n",
    "        if u[VEL_Y + idx] > -ss.V_MAX && u[VEL_Y + idx] < ss.V_MAX\n",
    "            du[VEL_Y + idx] = -ss.V_DAMP * u[VEL_Y + idx] + (v_tgt[2] - u[VEL_Y + idx]) / ss.MASS + fs[2,k] / ss.MASS\n",
    "        end\n",
    "        ss.v_old[1,k] = clamp(u[VEL_X + idx] + ss.v_bias[1,k], -ss.V_MAX, ss.V_MAX)\n",
    "        ss.v_old[2,k] = clamp(u[VEL_Y + idx] + ss.v_bias[2,k], -ss.V_MAX, ss.V_MAX)\n",
    "        du[POS_X + idx] = ss.v_old[1,k]\n",
    "        du[POS_Y + idx] = ss.v_old[2,k]\n",
    "    end\n",
    "    return du\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "528b8e1b-fedc-46c3-a19e-e7b0503ecf8b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function SoftKilobot_Step!(du, u, ss, t)\n",
    "    # Calculate forces\n",
    "    fs = SoftKilobot_CalcForces(u, ss)\n",
    "    # Calculate target speeds using algorithm\n",
    "    # TODO\n",
    "    v_tgt = [0.1; 0.0]\n",
    "    # Update states\n",
    "    du = SoftKilobot_UpdateState(du, u, ss, v_tgt, fs)\n",
    "    # All done\n",
    "    return du\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7c546c42-f48b-4362-82f4-7522034a82ef",
   "metadata": {},
   "source": [
    "# Plotting"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "88041759-7c44-467c-adca-e5371d16ad1a",
   "metadata": {},
   "outputs": [],
   "source": [
    "PLOT_SIDE  = 2.0;\n",
    "PLOT_START = 0.4;\n",
    "PLOT_END   = 1.6;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "20c98e73-ce62-49b2-bc38-a932d077a4b7",
   "metadata": {},
   "outputs": [],
   "source": [
    "function SoftKilobot_Plot(x::Matrix{Float64}, t::Float64; vel=false, springs=false)\n",
    "    # General settings\n",
    "    p = plot(xlims=(0,2), ylims=(0,2), title=string(\"t = \", t), legend=:none)\n",
    "    # Start and end lines\n",
    "    plot!(p, [PLOT_START PLOT_END; PLOT_START PLOT_END], [0 0; PLOT_SIDE PLOT_SIDE])\n",
    "    # Robots as circles\n",
    "    scatter!(p, x[POS_X+1,:], x[POS_Y+1,:])\n",
    "    # Plot velocities\n",
    "    if(vel)\n",
    "        for i in 1:ss.n\n",
    "            plot!(p, [x[POS_X+1,i]; (x[POS_X+1,i]+x[VEL_X+1,i])], [x[POS_Y+1,i]; (x[POS_Y+1,i]+x[VEL_Y+1,i])])\n",
    "            plot!(p, [x[POS_X+1,i]; (x[POS_X+1,i]+ss.v_bias[1,i])], [x[POS_Y+1,i]; (x[POS_Y+1,i]+ss.v_bias[2,i])])\n",
    "            plot!(p, [x[POS_X+1,i]; (x[POS_X+1,i]+ss.v_old[1,i])], [x[POS_Y+1,i]; (x[POS_Y+1,i]+ss.v_old[2,i])])\n",
    "        end\n",
    "    end\n",
    "    # Plot springs as segments\n",
    "    if(springs)\n",
    "        for i in 1:ss.n\n",
    "            for j in ss.neighs[i]\n",
    "                if(i < j)\n",
    "                    plot!(p, [x[POS_X+1,i]; x[POS_X+1,j]], [x[POS_Y+1,i]; x[POS_Y+1,j]])\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return p\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ea7fee26-bf02-426a-97b9-044fcf5a7eed",
   "metadata": {},
   "source": [
    "# Demo Without Noise"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "e2151855-7b53-4166-9f78-c65be56a85b7",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Control frequency\n",
    "DELTA_T = 1.0 / 31.0;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dd80a8a6-8205-4a86-8ccc-df58d34eef80",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Duration in seconds\n",
    "DURATION = 120;\n",
    "\n",
    "# Initialization\n",
    "ss, u0 = SoftKilobot_Init(\n",
    "    [0.4,1], # Center of the robot distribution\n",
    "    135.0,   # Orientation of robot distribution (degrees)\n",
    "    7,       # Robots per side\n",
    "    0.024,   # Robot mass\n",
    "    0.015,   # Max speed\n",
    "    0.01,    # Speed damping\n",
    "    0.031,   # Spring rest length\n",
    "    0.6,     # Spring stiffness\n",
    "    0.1,     # Spring damping\n",
    "    nothing, # Noise factor on velocity\n",
    "    nothing, # Noise on velocity bias vector length\n",
    "    nothing) # Noise on velocity bias vector angle\n",
    "\n",
    "# Execution\n",
    "u = vec(u0)\n",
    "@gif for i in 0:(DURATION/DELTA_T)\n",
    "    tspan = (i * DELTA_T, (i+1) * DELTA_T);\n",
    "    prob = ODEProblem(SoftKilobot_Step!, u, tspan, ss);\n",
    "    sol = solve(prob)\n",
    "    for j in 1:length(sol.t)\n",
    "        SoftKilobot_Plot(reshape(sol.u[j], FIELDS, ss.n), sol.t[j])\n",
    "    end\n",
    "    u = sol.u[end]\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9d4cb83e-dc84-423d-9ba7-cc86521af4aa",
   "metadata": {},
   "source": [
    "# Demo With Noise"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7743a20b-0755-4f4b-8c30-aefb823ec275",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Duration in seconds\n",
    "DURATION = 120;\n",
    "\n",
    "Random.seed!(123)\n",
    "\n",
    "# Initialization\n",
    "ss, u0 = SoftKilobot_Init(\n",
    "    [0.4,1],                          # Center of the robot distribution\n",
    "    135.0,                            # Orientation of robot distribution (degrees)\n",
    "    7,                                # Robots per side\n",
    "    0.024,                            # Robot mass\n",
    "    0.015,                            # Max speed\n",
    "    0.01,                             # Speed damping\n",
    "    0.031,                            # Spring rest length\n",
    "    0.6,                              # Spring stiffness\n",
    "    0.1,                              # Spring damping\n",
    "    Some(Normal(0.9,0.1)),            # Noise factor on velocity\n",
    "    Some(Normal(0.005,0.005)),        # Noise on velocity bias vector length\n",
    "    Some(Normal(0.0, 45.0*pi/180.0))) # Noise on velocity bias vector angle\n",
    "\n",
    "# Execution\n",
    "u = vec(u0)\n",
    "@gif for i in 0:(DURATION/DELTA_T)\n",
    "    tspan = (i * DELTA_T, (i+1) * DELTA_T);\n",
    "    prob = ODEProblem(SoftKilobot_Step!, u, tspan, ss);\n",
    "    sol = solve(prob)\n",
    "    for j in 1:length(sol.t)\n",
    "        SoftKilobot_Plot(reshape(sol.u[j], FIELDS, ss.n), sol.t[j], vel=false, springs=false)\n",
    "    end\n",
    "    u = sol.u[end]\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.1",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
